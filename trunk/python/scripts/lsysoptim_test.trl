transsys cycler
{
  factor A { decay: 0.0; }

  gene agene
  {
    promoter
    {
      constitutive: 1.0;
    }
    product
    {
      default: A;
    }
  }
}

lsys simplebrancher
{
  diffusionrange: 2;

  symbol meristem(cycler);
  symbol shoot_piece;
  symbol left;
  symbol right;
  symbol [;
  symbol ];

  axiom meristem();

  rule grow
  {
    meristem(t) : t.A >= 5.0 -->
	[ left meristem(transsys t: A = 0.0) ]
	[ right meristem(transsys t: A = 0.0) ]
	shoot_piece meristem(transsys t: A = 0.0)
  }

  graphics
  {
    meristem
    {
      move(0.2);
      color(0.6, 0.6, 0.6);
      sphere(0.2);
      move(0.2);
    }
    shoot_piece
    {
      move(0.5);
      color(0.7, 0.7, 0.7);
      cylinder(0.1, 1.0);
      move(0.5);
    }
    left { turn(20); }
    right { turn(-20); }
    [ { push(); }
    ] { pop(); }
  }
}
